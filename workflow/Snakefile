import os

from snakemake.utils import min_version

min_version('8.11')


configfile: os.environ['GROUP_HOME'] + '/config/public_data.json'
configfile: 'config/my_data.json'
configfile: 'config/params.json'


module widevariant:
    snakefile: github('fm-key-lab/widevariant_module', path='workflow/Snakefile', tag='0.0.4-alpha')
    config: config


include: 'rules/samplesheet.smk'
include: 'rules/mapping.smk'
include: 'rules/variants.smk'
include: 'rules/sequence_typing.smk'
include: 'rules/phylogeny.smk'
include: 'rules/prepare_results.smk'
include: 'rules/prepare_report.smk'


wildcard_constraints:
    donors=config['wildcards']['donors'],
    sequencing=config['wildcards']['sequencing'],
    species=config['wildcards']['species']


# TODO: Duplicate ID errors for ID=B001_4186 (sample IDs 75, 76, 821, and 822)
# TODO: bwa mem throws "paired reads have different names" error for 
#       samples 75, 76, 821, and 822. Temporarily excluding.
#       sample 75 also has `Duplicate entry "NC_002695.2" in sam header`
#       Real issue is with ID name collision.
EXCLUDE = [75, 76, 821, 822]


rule all:
    input:
        expand(
            'results/{species}/all_variants.duckdb',
            species=config['wildcards']['species'].split('|')
        )
    localrule: True
    default_target: True


onstart:
    shell('find logs -type f ! -path "logs/notebooks/*" -delete')
    shell('rm -f .nextflow.log*')


onsuccess:
    shell(f'mkdir -p "/r/t/thosi/{config['run_id']}"')
    shell(f'rsync -au --progress report "/r/t/thosi/{config['run_id']}/"')

    # Clean up compiled report
    shell(f'rm -f "report/main.{config['run_id']}.$(date +"%Y%m%d").pdf"')
    shell(f'touch report/main.pdf')
    shell(f'mv report/main.pdf "report/main.{config['run_id']}.$(date +"%Y%m%d").pdf"')